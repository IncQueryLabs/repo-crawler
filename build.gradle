plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.60'
    id 'io.vertx.vertx-plugin' version '0.9.0'
}

def timestampFormat = new java.text.SimpleDateFormat('yyyyMMdd.HHmmss')
timestampFormat.timeZone = TimeZone.getTimeZone("UTC")
Date buildTime = new Date()
ext.releaseProp = hasProperty("release") ? getProperty("release") : false

ext{
    buildTimestamp = timestampFormat.format(buildTime)
    baseVersion = '1.2'
}

def computedVersion = baseVersion + '-' + buildTimestamp
if (releaseProp.toBoolean()) {
    computedVersion = baseVersion
}

group 'com.incquerylabs'
version computedVersion

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    compile 'io.vertx:vertx-web'
    compile 'io.vertx:vertx-web-client'
}

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}
compileTestKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

application {
    mainClassName = 'com.incquerylabs.twc.repo.crawler.CrawlerKt'
}

vertx {
    launcher = 'com.incquerylabs.twc.repo.crawler.CrawlerKt'
}

wrapper {
    gradleVersion = '5.6.1'
    distributionType = 'all'
}